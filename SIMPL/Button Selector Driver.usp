Digital_Input SelectRoundRectanglePressed;
Digital_Input SelectCirclePressed;
Digital_Input SelectRectanglePressed;
Digital_Input SelectTabPressed;
Digital_Input SelectOvalPressed;
Digital_Input _SKIP_;
Digital_Input SelectSimplePressed;
Digital_Input SelectIconsPressed;
Digital_Input SelectIconsTextPressed;
Digital_Input _SKIP_;


Digital_Output SelectRoundRectangleSelected;
Digital_Output SelectCircleSelected;
Digital_Output SelectRectangleSelected;
Digital_Output SelectTabSelected;
Digital_Output SelectOvalSelected;
Digital_Output _SKIP_;
Digital_Output SelectSimpleSelected;
Digital_Output SelectIconsSelected;
Digital_Output SelectIconsTextSelected;
Digital_Output _SKIP_;
Digital_Output Widgets[15];
Digital_Output _SKIP_;
Analog_Output NumberOfShapesButtons;
Analog_Output NumberOfFeaturesButtons;
Analog_Output _SKIP_;
String_Output RoundRectangleTitle;
String_Output CircleTitle;
String_Output RectangleTitle;
String_Output TabTitle;
String_Output OvalTitle;
String_Output _SKIP_;
String_Output SimpleTitle;
String_Output IconsTitle;
String_Output IconsTextTitle;

Integer ShapeSelected;
Integer FeatureSelected;

Function SelectWidget() {
	Integer selectedWidget, i;

	selectedWidget = (ShapeSelected - 1) * 3 + FeatureSelected;
	For (i = 1 to 15) {
		If (i = selectedWidget) {
			Widgets[i] = 1;
		}
		Else {
			Widgets[i] = 0;
		}
	}
}

// #Region Shape
Function UnselectAllShapeButtons() {
	SelectRoundRectangleSelected = 0;
	SelectCircleSelected = 0;
	SelectRectangleSelected = 0;
	SelectTabSelected = 0;
	SelectOvalSelected = 0;
}
Function SelectShapeButton() {
	CSwitch (ShapeSelected) {
		Case (1): {
			SelectRoundRectangleSelected = 1;
			Break;
		}
		Case (2): {
			SelectCircleSelected = 1;
			Break;
		}
		Case (3): {
			SelectRectangleSelected = 1;
			Break;
		}
		Case (4): {
			SelectTabSelected = 1;
			Break;
		}
		Case (5): {
			SelectOvalSelected = 1;
			Break;
		}
	}
}
Function SetShape() {
	UnselectAllShapeButtons();
	SelectShapeButton();
	SelectWidget();
}
Push SelectRoundRectanglePressed {
	ShapeSelected = 1;
	SetShape();
}
Push SelectCirclePressed {
	ShapeSelected = 2;
	SetShape();
}
Push SelectRectanglePressed {
	ShapeSelected = 3;
	SetShape();
}
Push SelectTabPressed {
	ShapeSelected = 4;
	SetShape();
}
Push SelectOvalPressed {
	ShapeSelected = 5;
	SetShape();
}
// #EndRegion


// #Region Feature
Function UnselectAllFeatureButtons() {
	SelectSimpleSelected = 0;
	SelectIconsSelected = 0;
	SelectIconsTextSelected = 0;
}
Function SetFeatureButton() {
	CSwitch (FeatureSelected) {
		Case (1): {
			SelectSimpleSelected = 1;
			Break;
		}
		Case (2): {
			SelectIconsSelected = 1;
			Break;
		}
		Case (3): {
			SelectIconsTextSelected = 1;
			Break;
		}
	}
}
Function SetFeature() {
	UnselectAllFeatureButtons();
	SetFeatureButton();
	SelectWidget();
}
Push SelectSimplePressed {
	FeatureSelected = 1;
	SetFeature();
}
Push SelectIconsPressed {
	FeatureSelected = 2;
	SetFeature();
}
// #EndRegion

Function Main() {
	NumberOfShapesButtons = 5;
	NumberOfFeaturesButtons = 3;

	ShapeSelected = 1;
	RoundRectangleTitle = "Round Rectangle";
	CircleTitle = "Circle";
	RectangleTitle = "Rectangle";
	TabTitle = "Tab";
	OvalTitle = "Oval";
	SetShape();

	FeatureSelected = 1;
	SimpleTitle = "Simple";
	IconsTitle = "Icons";
	IconsTextTitle = "Icons + Text";
	SetFeature();
}
Digital_Input SelectRectanglePressed;
Digital_Input SelectRoundRectanglePressed;
Digital_Input SelectTabPressed;
Digital_Input _SKIP_;
Digital_Input SelectSimplePressed;
Digital_Input SelectIconsPressed;
Digital_Input SelectIconsTextPressed;
Digital_Input _SKIP_;


Digital_Output SelectRectangleSelected;
Digital_Output SelectRoundRectangleSelected;
Digital_Output SelectTabSelected;
Digital_Output _SKIP_;
Digital_Output SelectSimpleSelected;
Digital_Output SelectIconsSelected;
Digital_Output SelectIconsTextSelected;
Digital_Output _SKIP_;
Digital_Output Widgets[9];
Digital_Output _SKIP_;
Analog_Output NumberOfShapesButtons;
Analog_Output NumberOfFeaturesButtons;
Analog_Output _SKIP_;
String_Output RectangleTitle;
String_Output RoundRectangleTitle;
String_Output TabTitle;
String_Output _SKIP_;
String_Output SimpleTitle;
String_Output IconsTitle;
String_Output IconsTextTitle;

Integer ShapeSelected;
Integer FeatureSelected;


Function SelectWidget() {
	Integer selectedWidget, i;

	selectedWidget = (ShapeSelected - 1) * 3 + FeatureSelected;
	For (i = 1 to 9) {
		If (i = selectedWidget) {
			Widgets[i] = 1;
		}
		Else {
			Widgets[i] = 0;
		}
	}
}

// #Region Shape
Function UnselectAllShapeButtons() {
	SelectRoundRectangleSelected = 0;
	SelectRectangleSelected = 0;
	SelectTabSelected = 0;
}
Function SetShapeButtons() {
	CSwitch (ShapeSelected) {
		Case (1): {
			SelectRoundRectangleSelected = 1;
			Break;
		}
		Case (2): {
			SelectRectangleSelected = 1;
			Break;
		}
		Case (3): {
			SelectTabSelected = 1;
			Break;
		}
	}
}
Function SetShape() {
	UnselectAllShapeButtons();
	SetShapeButtons();
	SelectWidget();
}
Push SelectRoundRectanglePressed {
	ShapeSelected = 1;
	SetShape();
}
Push SelectRectanglePressed {
	ShapeSelected = 2;
	SetShape();
}
Push SelectTabPressed {
	ShapeSelected = 3;
	SetShape();
}
// #EndRegion

// #Region Feature
Function UnselectFeatureButtons() {
	SelectSimpleSelected = 0;
	SelectIconsSelected = 0;
	SelectIconsTextSelected = 0;
}
Function SetFeatureButtons() {
    CSwitch (FeatureSelected) {
        Case (1): {
            SelectSimpleSelected = 1;
            Break;
        }
        Case (2): {
            SelectIconsSelected = 1;
            Break;
        }
        Case (3): {
            SelectIconsTextSelected = 1;
            Break;
        }
    }
}
Function SetFeature() {
    UnselectFeatureButtons();
    SetFeatureButtons();
    SelectWidget();
}
Push SelectSimplePressed {
    FeatureSelected = 1;
    SetFeature();
} 
Push SelectIconsPressed {
    FeatureSelected = 2;
    SetFeature();
}
    Push SelectIconsTextPressed {
    FeatureSelected = 3;
    SetFeature();
}
// #EndRegion

Function Main() {
	NumberOfShapesButtons = 3;
	NumberOfFeaturesButtons = 3;

	SelectRectangleSelected = 1;
	ShapeSelected = 2;
	RoundRectangleTitle = "Round Rectangle";
	RectangleTitle = "Rectangle";
	TabTitle = "Tab";

	SelectSimpleSelected = 1;
	FeatureSelected = 1;
	SimpleTitle = "Simple";
	IconsTitle = "Icons";
	IconsTextTitle = "Icons + Text";
	SelectWidget();
}